/**
 *Copyright (c) 2018 PROPHESSOR
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */

// Arms in the hands of Chaser

Actor ChaserFist : Fist {
	Inventory.RestrictedTo "Chaser"
	Weapon.Kickback 0

	States {	
		Fire:
				PUNG B 4
				PUNG C 2 A_CustomPunch(1)
				PUNG C 2
				PUNG B 5 A_ReFire
				Goto Ready
	}
}

Actor ChaserKick : Kick {
	Inventory.RestrictedTo "Chaser"
	Weapon.Kickback 0

	States {	
		Ready:
			KICK J 0 A_WeaponReady
			Goto Fire
		Deselect:
			KICK J 0 A_Lower
			Loop
		Select:
			KICK J 0 A_Raise
			Loop
		Fire:
			KICK K 4
			KICK L 4
			KICK M 4
			KICK N 2 A_PlaySound("weapon/kickmiss", CHAN_5, 1.0, 0.0)
			KICK O 2 A_Recoil(5)
			KICK P 2 
			KICK Q 2 A_CustomPunch(2, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown")
			KICK R 3 A_Recoil(5)
			KICK S 2 
			KICK T 2 A_TakeInventory("ChaserKick")
			Stop
	}
}

Actor ChaserChainsaw : Chainsaw {
	Inventory.RestrictedTo "Chaser"

	States {
		Fire:
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 2.0 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.5 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.2 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.0 )

			SAWG B 0 A_ReFire
			Goto Ready
	}
}

Actor ChaserPistol : Pistol {
	Inventory.RestrictedTo "Chaser"
}

Actor ChaserShotgun : Shotgun {
	Inventory.RestrictedTo "Chaser"
}

Actor ChaserSSG : SuperShotgun {
	Inventory.RestrictedTo "Chaser"
}

Actor ChaserChaingun : Chaingun {
	Inventory.RestrictedTo "Chaser"
}

Actor ChaserRL : RocketLauncher {
	Inventory.RestrictedTo "Chaser"

	States {
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_SetPitch( Pitch - 30 )
			MISG B 0 A_Recoil( 5 )
			MISG B 12 A_FireMissile
			MISG B 0 A_ReFire
			Goto Ready
	}
}

Actor ChaserPlasmagun : PlasmaRifle {
	Inventory.RestrictedTo "Chaser"

	States {
		Fire:
			PLSG A 8 A_FirePlasma
			BFGF AAAA 0 Bright A_SetAngle( Angle + FRandom( -1.5, 1.5 ) )
			BFGF AAAA 0 Bright A_SetPitch( Pitch + FRandom( -1.35, 1.35 ) )
			BFGF AAAA 0 Bright A_Recoil( FRandom( 0.02, 0.35 ) )
			PLSG B 20 A_ReFire
			Goto Ready
	}
}

Actor ChaserBFG : BFG9000 {
	Inventory.RestrictedTo "Chaser"
	Weapon.SelectionOrder 1

	States {
		Flash:
			BFGF A 3 Bright A_Light1
			BFGF A 0 A_PlaySound( "chaser/aaaa", CHAN_5, 1, 0 )
			BFGF AAAA 0 Bright A_SetAngle( Angle + FRandom( -1.5, 1.5 ) )
			BFGF AAAA 0 Bright A_SetPitch( Pitch + FRandom( -1.35, 1.35 ) )
			BFGF AAAA 0 Bright A_Recoil( FRandom( 10.02, 10.35 ) )
			BFGF AAAA 0 Bright A_Quake( 1, 6, 0, 0 )
			BFGF B 0 Bright A_Light2
			BFGF B 0 Bright A_SetBlend( "44FF44", 0.5, Random( 20, 35 ) )
			BFGF B 0 Bright A_SetPitch( Pitch - 30/* FRandom( 30.5, 50 ) */ )
			BFGF B 0 Bright A_Recoil( FRandom( 10.5, 20.5 ) )
			BFGF B 0 Bright A_Quake( 1, 6, 0, 0 )
			Goto LightDone
	}
}
