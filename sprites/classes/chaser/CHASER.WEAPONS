/**
 *Copyright (c) 2018 PROPHESSOR
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */

// Arms in the hands of Chaser

Actor ChaserFist : Fist {
	Inventory.RestrictedTo "Chaser"
	Weapon.Kickback 0
	Inventory.PickupSound "chaser/pickup"

	States {	
		Fire:
				PUNG B 4
				PUNG C 2 A_CustomPunch(1)
				PUNG C 2
				PUNG B 5 A_ReFire
				Goto Ready
	}
}

Actor ChaserKick : Kick {
	Inventory.RestrictedTo "Chaser"
	Weapon.Kickback 0
	Inventory.PickupSound "chaser/pickup"

	States {	
		Ready:
			KICK J 0 A_WeaponReady
			Goto Fire
		Deselect:
			KICK J 0 A_Lower
			Loop
		Select:
			KICK J 0 A_Raise
			Loop
		Fire:
			KICK K 4
			KICK L 4
			KICK M 4
			KICK N 2 A_PlaySound("weapon/kickmiss", CHAN_5, 1.0, 0.0)
			KICK O 2 A_Recoil(5)
			KICK P 2 
			KICK Q 2 A_CustomPunch(2, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown")
			KICK R 3 A_Recoil(5)
			KICK S 2 
			KICK T 2 A_TakeInventory("ChaserKick")
			Stop
	}
}

Actor ChaserChainsaw : Chainsaw {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 2.0 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.5 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.2 )
			SAWG AB 2 A_Saw
			SAWG AAA 0 A_SetAngle( FRandom( 2.2, -2.2 ) + Angle )
			SAWG AAA 1 A_SetPitch( Pitch - 1.0 )

			SAWG B 0 A_ReFire
			Goto Ready
	}
}

Actor ChaserPistol : Pistol {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"
}

Actor ChaserShotgun : Shotgun {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			SHTG A 1
			SHTG A 0 A_FireBullets (5.6, 5.6, 5, 1, "BulletPuff")
			SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
			SHTG A 0
			SHTG A 4 A_GunFlash
			SHTG A 2
			SHTG AA 3
			SHTG A 1
			SHTG A 5 A_ReFire
			Goto Ready
	}
}

Actor ChaserSSG : SuperShotgun {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			SHT2 A 3
			SHT2 A 7 A_FireShotgun2
			SHT2 B 7
			SHT2 C 7 A_CheckReload
			SHT2 D 7 A_OpenShotgun2
			CCHT E 5 
			CCHT F 0 A_PlaySound("chaser/weapons/ssg/hit", 6, 1.0)
			CCHT FFG 5
			CCHT HHHH 5
			CCHT I 0 A_PlaySound("chaser/weapons/ssg/hit", 5, 1.0)
			CCHT I 5
			CCHT JJJ 5
			CCHT F 0 A_PlaySound("chaser/weapons/ssg/hit", 6, 1.0)
			CCHT FG 5
			CCHT F 0 A_PlaySound("chaser/eee", CHAN_7)
			CCHT HH 5
			CCHT I 0 A_PlaySound("chaser/weapons/ssg/hit", 5, 1.0)
			CCHT I 5 
			CCHT HHHHHHH 5
			CCHT F 0 A_PlaySound("chaser/weapons/ssg/hit", 6, 1.0)
			CCHT FG 3
			CCHT F 0 A_PlaySound("chaser/weapons/ssg/hit", 6, 1.0)
			CCHT FG 3
			CCHT HH 5
			CCHT I 0 A_PlaySound("chaser/weapons/ssg/hit",5, 1.0)
			CCHT IIJJKKLLMM 5
			CCHT I 0 A_PlaySound("chaser/blin", CHAN_7)
			CCHT D 0 A_PlaySound("chaser/weapons/ssg/close",5,0.8)
			SHT2 DBCCCBB 3
			SHT2 A 5 A_ReFire
			Goto Ready
	}
}

Actor ChaserChaingun : Chaingun {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"
}

Actor ChaserRL : RocketLauncher {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_SetPitch( Pitch - 30 )
			MISG B 0 A_Recoil( 5 )
			STOM ABCDE 2 Bright A_Quake( 1, 6, 0, 0 )
			MISG B 12 A_FireMissile
			MISG B 0 A_ReFire
			Goto Ready
	}
}

Actor ChaserPlasmagun : PlasmaRifle {
	Inventory.RestrictedTo "Chaser"
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			PLSG A 8 A_FirePlasma
			BFGF AAAA 0 Bright A_SetAngle( Angle + FRandom( -1.5, 1.5 ) )
			BFGF AAAA 0 Bright A_SetPitch( Pitch + FRandom( -1.35, 1.35 ) )
			BFGF AAAA 0 Bright A_Recoil( FRandom( 0.02, 0.35 ) )
			PLSG B 20 A_ReFire
			Goto Ready
	}
}

Actor CFG9000Ball {
	PROJECTILE
	+FORCERADIUSDMG
	+THRUSPECIES

	Speed 10 // 18
	Radius 20
	Height 30
	Damage 500
	Alpha 0.95
	Scale 2.0
	Alpha 0.85
	DeathSound ""
	Species "Player"
	Obituary "$OB_MPBFG_BOOM"
	RenderStyle Translucent

	States {
		Spawn:
			TNT1 A 0 A_Jump(128, "DTBFGSFX")
			TNT1 A 0 A_AlertMonsters
			BFS1 ABAD 1 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Loop
		DTBFGSFX:
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySoundEx("chaser/weapons/bfg/ball", "Voice", 1)
			BFS1 ABAB 1 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			Goto Spawn
		Death:
			TNT1 A 0 A_SetTranslucent(0.75, 1)
			TNT1 A 0 Radius_Quake(5, 60, 0, 10, 0)
			BFS1 ABABABABABABABAB 2 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			BFS1 A 0 A_StopSoundEx("Voice")
			BFS1 A 0 A_PlaySoundEx("weapons/bfgx", "Voice", 0)
			BFS1 A 0 A_Explode(480, 240)
			TNT1 A 0 Radius_Quake(15, 20, 0, 20, 0)
			BFE1 ABACBDCEEF 4 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			BFS1 A 0 A_PlaySoundEx("weapons/bfgx", "Voice", 0)
			TNT1 A 0 A_SpawnItemEx("BFGBall", 0, 0, 0, 0, 0, -5, 0, SXF_SETMASTER)
			BFS1 A 0 A_Explode(480, 240)
			TNT1 A 0 Radius_Quake(15, 20, 0, 20, 0)
			BFE1 ABACBDCEEF 4 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			BFS1 A 0 A_PlaySoundEx("weapons/bfgx", "Voice", 0)
			TNT1 A 0 A_SpawnItemEx("BFGBall", 0, 0, 0, 0, 0, -5, 0, SXF_SETMASTER)
			BFS1 A 0 A_Explode(480, 240)
			TNT1 A 0 Radius_Quake(15, 20, 0, 20, 0)
			TNT1 A 0 A_SpawnItemEx("BFGBall", 0, 0, 0, 0, 0, -5, 0, SXF_SETMASTER)
			BFE1 ABACBDCEEF 4 Bright A_SpawnItemEx("PlayerDTBFGProjectileAttack", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER)
			TNT1 A 25 Bright
			Stop
	}
}

ACTOR PlayerDTBFGProjectileAttack {
	PROJECTILE
	+LOOKALLAROUND
	+ISMONSTER
	+QUICKTORETALIATE
	+NOTARGET
	+FRIENDLY
	+NOCLIP

	States {
		Spawn:
			TNT1 A 0
			TNT1 AAAAA 1 A_Look
			Stop
		See:
			TNT1 A 0
			TNT1 A 1 A_JumpIfCloser(1250, "Attack")
			Stop
		Attack:
			TNT1 A 0
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_MissileAttack
			TNT1 A 1 A_CustomRailgun(10, 0, NONE, "Green", 1, 1, 2)
			Goto Death	
		Death:
			TNT1 A 1
			Stop
	}
}


Actor ChaserBFG : BFG9000 {
	Inventory.RestrictedTo "Chaser"
	Weapon.SelectionOrder 1
	Weapon.AmmoUse 100
	Inventory.PickupSound "chaser/pickup"

	States {
		Fire:
			BFGG A 20 A_BFGSound
			BFGG B 10 A_GunFlash
			BFGF A 0 A_PlaySound( "chaser/aaaa", CHAN_5, 1, 0 )
			BFGF AAAA 0 Bright A_SetAngle( Angle + FRandom( -1.5, 1.5 ) )
			BFGF AAAA 0 Bright A_SetPitch( Pitch + FRandom( -1.35, 1.35 ) )
			BFGF AAAA 0 Bright A_Recoil( FRandom( 10.02, 10.35 ) )
			BFGF AAAA 0 Bright A_Quake( 6, 6, 0, 0 )
			BFGG B 1 A_FireCustomMissile("CFG9000Ball", 0, 1)
			BFGF B 0 Bright A_SetBlend( "44FF44", 0.5, Random( 20, 55 ) )
			BFGF B 0 Bright A_SetPitch( Pitch - 30/* FRandom( 30.5, 50 ) */ )
			BFGF B 0 Bright A_Recoil( FRandom( 10.5, 20.5 ) )
			BFGF B 0 Bright A_Quake( 6, 6, 0, 0 )
			STOM ACE 1 Bright A_Quake( 6, 6, 0, 0 )
			STOM EEEE 2 Bright A_Recoil( FRandom( 10.5, 20.5 ) )
			BFGG B 20 A_ReFire
			Goto Ready
		Flash:
			BFGF A 3 Bright A_Light1
			BFGF B 0 Bright A_Light2
			Goto LightDone
	}
}
