/**
*Copyright (c) 2018 PROPHESSOR
*
*This software is released under the MIT License.
*https://opensource.org/licenses/MIT
*/

// Arms in the hands of Nomakh Thunder

Actor NomakhFist : Fist {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhKick : Kick {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Weapon.Kickback 350
	Inventory.PickupSound "nomakh/pickup"

	States {
		Ready:
			KICK A 0 A_WeaponReady
			Goto Fire
		Deselect:
			KICK A 0 A_Lower
			Loop
		Select:
			KICK A 0 A_Raise
			Loop
		Fire:
			KICK B 1
			KICK C 1
			KICK D 1
			KICK E 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
			KICK F 1
			KICK G 1 A_CustomPunch(0, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown")
			KICK H 3
			KICK H 1 A_CustomPunch(20, false, 0, "BulletPuff", 64, 0, 0, "", "nomakh/boom", "weapon/kickmiss")
			KICK I 3
			KICK G 1 A_Recoil(2)
			KICK B 1 A_TakeInventory("NomakhKick")
			Stop
	}
}

Actor NomakhChainsaw : Chainsaw {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhPistol : Pistol {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhShotgun : Shotgun {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

// Actor NomakhSSG : SuperShotgun {
// 	+INVENTORY.RESTRICTABSOLUTELY

// 	Inventory.RestrictedTo "Nomakh"
// 	Inventory.PickupSound "nomakh/pickup"
// }

/*
	CREDITS:
	Random Name Generator - this gun name is actually quite spiffy!
	Original Quadshot - Logan
	Beatiful angled Quadshot sprite - Cage!i!i!i!i!i! (added those !'s and i's on his special request - I owe him after all. :P)
	NeuralStunner - Three-dimensional recoil
	Fire sound - Propably Killing Floor, but I'm not sure. Ripped by Uboa. Great thanks for that, it adds insane amount of kick to the gun!
	I am not quite sure from where are rest sounds; which game are the original sounds, but those are modified ones from NeoCore. Ripped by Sangelothi.
	Gunflash - Bane, props to you for this one - it's really damn nice gunflash - it became my new staple one.

	Compared to Erasus, it's slower in terms of fire rate, and it doesn't have casing-spewing attachment. It also has horrible recoil,
	But that can be used in positive way, since you can use it as a make-shift rocket-jumping device. It also makes up with it's
	flaws by shooting twice as much pellets per shot, with even bigger power, and slightly tightened horizontal spread.
	Also, it has a nasty ability to deal horribly large damage on point-blank shot. Give Cyberdemon a taste!
*/

Actor NomakhSSGReloadCounter : Inventory {
	Inventory.MaxAmount 4
}

Actor NomakhSSG : Weapon 24530 {
	+INVENTORY.RESTRICTABSOLUTELY
	+NOALERT

	Weapon.Kickback 1000
	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
	Inventory.PickupMessage "You've got the quad shotgun!"
	Weapon.UpSound "nomakh/weapons/ssg/Up"
	Weapon.SelectionOrder 6
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse 16
	Weapon.AmmoGive 16
	Weapon.SlotNumber 3
	Obituary "%o was blown away and smeared on wall by Nomakh's 4x4 shotgun."

	var int user_count;

	States {
		Spawn: 
			SSHG X -1
			Stop
		Ready:
			SSH4 B 0 A_TakeInventory("NomakhSSGReloadCounter", 4)
			SSH4 B 1 A_WeaponReady
			Loop
		Select:
			SSH4 BBBBBBBBBB 0 A_Raise(12)
			SSH4 MLKJIHGFEDCB 1
			Stop
		Deselect:
			SSHG A 0 A_ZoomFactor(1)
			SSH4 BCDEFGHIJKLM 1
			SSH4 MMMMMMMM 0 A_Lower(12)
			Loop
		Fire:
			TNT1 A 0 A_PlaySound("nomakh/weapons/ssg/Deploy",5,0.65)
			
			SSH4 A 15

			SSF4 A 2 Bright
			SSF4 A 0 A_GunFlash
			SSF4 A 0 A_AlertMonsters
			SSF4 A 0 A_ChangeVelocity (Cos(Pitch) * -116, 0, Sin(Pitch) * 116,CVF_RELATIVE)
			SSF4 A 0 A_Quake(9,18,0,64,none)

			SSF4 A 0 A_PlaySound("nomakh/weapons/ssg/Fire",1,1.0)
			SSF4 A 0 A_PlaySound("nomakh/weapons/ssg/Alt",6,1.0)
			SSF4 A 0 A_PlaySound("nomakh/boom",7,1.0)
			SSF4 A 0 A_FireBullets(25, 7.5, 128, 15, "BulletPuff")

			SSH4 B 1 A_ZoomFactor(0.9)	// TODO: Сделать компактней
			SSH4 B 0 A_SetPitch(Pitch-6)
			SSH4 D 1 A_ZoomFactor(0.8)
			SSH4 D 0 A_SetPitch(Pitch-5)
			SSH4 F 1 A_ZoomFactor(0.7)
			SSH4 F 0 A_SetPitch(Pitch-4)
			SSH4 H 1 A_ZoomFactor(0.71)
			SSH4 H 0 A_SetPitch(Pitch-1)
			SSH4 J 1 A_ZoomFactor(0.72)
			SSH4 H 0 A_SetPitch(Pitch-0.5)
			SSH4 L 1 A_ZoomFactor(0.73)
			SSH4 M 1 A_ZoomFactor(0.74)
			TNT1 A 1
			TNT1 A 1 A_ZoomFactor(0.75)
			TNT1 A 1 A_ZoomFactor(0.775)
			TNT1 A 1 A_ZoomFactor(0.8)
			TNT1 A 1 A_ZoomFactor(0.825)
			TNT1 A 1 A_ZoomFactor(0.85)
			TNT1 A 1 A_ZoomFactor(0.875)
			SSHO R 1 A_ZoomFactor(0.885)
			SSHO Q 1 A_ZoomFactor(0.895)
			SSHO P 1 A_ZoomFactor(0.9)
			SSHO O 1 A_ZoomFactor(0.91)
			SSHO N 1 A_ZoomFactor(0.92)
			SSHO M 1 A_ZoomFactor(0.93)
			SSHO L 1 A_ZoomFactor(0.94)
			SSHO K 1 A_ZoomFactor(0.95)
			SSHO J 1 A_ZoomFactor(0.96)
			SSHO I 1 A_ZoomFactor(0.97)
			SSHO H 1 A_ZoomFactor(0.98)
			SSHO G 1 A_ZoomFactor(0.99)
			SSHO F 1 A_ZoomFactor(1	)
			SSHO EDCBA 1
			SSHN GFEDCBA 1
			SSHG A 0 A_CheckReload 
			SSHG A 4 A_PlaySound("nomakh/cybr", CHAN_7, 1.0)
			Goto Reload
		Reload:	
			SSHN ABDEG 1
			SSHO ABDEIK 1
			SSHO M 1 A_PlaySound("nomakh/weapons/ssg/Open",5,0.3)
			SSHO NOQRTUWX 1
			SSHL A 3
			SSHL BCDE 1
			SSHL F 1 A_PlaySound("nomakh/weapons/ssg/Load",6,0.2)
			SSHL GHIJ 1
			SSHL K 2
			SSHL LMNO 1
			SSHL P 1 A_PlaySound("nomakh/weapons/ssg/Load",5,0.2)
			SSHL QRSTU 1
			SSHL V 2
			SSHC ABDE 1
			SSHO R 0 A_PlaySound("nomakh/weapons/ssg/Close",6,0.3)
			SSHO RQONKI 1
			SSHN GFDCB 1
			SSHG A 2 A_GiveInventory("NomakhSSGReloadCounter", 1)
			SSHG A 0 A_JumpIfInventory("NomakhSSGReloadCounter", 4, "Ready")
			Loop
		Flash:
			TNT1 A 3 Bright
			TNT1 A 2 Bright
			Goto LightDone 
		}
}

Actor NomakhChaingun : Chaingun {
	+INVENTORY.RESTRICTABSOLUTELY

	Scale 1.5

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
	Weapon.AmmoUse 5
	Translation "80:95=240:247", "96:111=232:239"

	States {
		Ready:
			NHGG A 1 A_WeaponReady
			Loop
		Deselect:
			NHGG A 1 A_Lower
			Loop
		Select:
			NHGG A 1 A_Raise
			Loop
		Fire:
			NHGG AB 8 A_PlaySound("nomakh/weapons/chaingun/start")
			NHGG AB 6 A_PlaySound("nomakh/weapons/chaingun/start")
			NHGG AB 4 A_PlaySound("nomakh/weapons/chaingun/start")
			NHGG ABAB 3
			NHGG ABAB 2
			Goto Hold
		Hold:
			NHGG A 0 A_PlaySound("nomakh/weapons/chaingun/fire", CHAN_WEAPON)
			NHGG A 0 A_GunFlash
			NHGG B 0 A_Recoil(0.5)
			NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
			NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
			NHGG A 2 A_FireBullets(6.6, 0, 3, 15, "BulletPuff")
			NHGG B 0 A_Recoil(0.5)
			NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
			NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
			NHGG B 0 A_PlaySound("nomakh/weapons/chaingun/fire", CHAN_WEAPON)
			NHGG B 0 A_GunFlash("Flash2")
			NHGG B 2 A_FireBullets(6.6, 0, 3, 15, "BulletPuff")
			NHGG B 0 A_Recoil(0.5)
			NHGG B 0 A_SetAngle(Angle + FRandom(-0.5, 0.5))
			NHGG B 0 A_SetPitch(Pitch + FRandom(-0.35, 0.35))
			NHGG B 0 A_ReFire
			Goto AfterHold
		AfterHold:
			NHGG AB 2 A_PlaySound("nomakh/weapons/chaingun/stop")
			NHGG AB 2
			NHGG A 4
			NHGG B 6 A_PlaySound("nomakh/weapons/chaingun/stop")
			NHGG A 8
			NHGG B 8 A_PlaySound("nomakh/weapons/chaingun/stop")
			Goto Ready
		Flash:
			NHGF A 4 Bright A_Light1
			Goto LightDone
		Flash2:
			NHGF B 4 Bright A_Light2
			Goto LightDone
		Spawn:
			MGUN A -1
			Stop
	}
}

Actor NomakhRL : RocketLauncher {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhPlasmagun : PlasmaRifle {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}

Actor NomakhBFG : BFG9000 {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "Nomakh"
	Inventory.PickupSound "nomakh/pickup"
}
