/**
 *Copyright (c) 2018 PROPHESSOR
 *
 *This software is released under the MIT License.
 *https://opensource.org/licenses/MIT
 */

// Arms in the hands of PROPHESSOR

Actor ProphessorFist : Fist {
	Inventory.RestrictedTo "PROPHESSOR"
}

Actor ProphessorKick : Kick {
	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.Kickback 228

	States {	
		Ready:
			KICK J 0 A_WeaponReady
			Goto Fire
		Deselect:
			KICK J 0 A_Lower
			Loop
		Select:
			KICK J 0 A_Raise
			Loop
		Fire:
			KICK K 1
			KICK L 1
			KICK M 1
			KICK N 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
			KICK O 1
			KICK P 1 
			KICK Q 1 A_CustomPunch(11, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown", "weapon/kickmiss")
			KICK R 3 A_Recoil(4)
			KICK S 1 
			KICK T 1 A_TakeInventory("ProphessorKick")
			Stop
	}
}

Actor ProphessorChainsaw : Chainsaw {
	Inventory.RestrictedTo "PROPHESSOR"
}

Actor ProphessorPistol : Pistol {
	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 100

	States {
		Fire:
			PISG A 4
			PISG B 1 A_FireBullets (5.6, 0, 1, 100, "BulletPuff")
			PISG C 1 A_GunFlash
			PISG C 15 A_SpawnItemEx("Backpack", 100, 0, 10, random(10, 20), 0, 0)
			PISG C 4
			PISG B 5 A_ReFire
			Goto Ready
	}
}

Actor ProphessorShotgun : Shotgun {
	Inventory.RestrictedTo "PROPHESSOR"
}

Actor ProphessorSSG : SuperShotgun {
	Inventory.RestrictedTo "PROPHESSOR"
}

Actor ProphessorChaingun : Chaingun {
	Inventory.RestrictedTo "PROPHESSOR"

	States {
		Fire:
			CHGG AB 2 A_FireCGun
			CHGG B 0 A_ReFire
			Goto Ready
	}
}

Actor ProphessorRL : RocketLauncher {
	Inventory.RestrictedTo "PROPHESSOR"

	States {
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_FireMissile
			MISG B 12 A_FireMissile
			MISG B 0 A_ReFire
			Goto Ready
	}
}

Actor ProphessorPlasmagun : PlasmaRifle {
	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 1

	States {
		Fire:
			PLSG A 1 A_FirePlasma
			BFGF AAAA 0 Bright A_Recoil( FRandom( 0.02, 0.35 ) )
			PLSG B 0 A_ReFire
			Goto Ready
	}
}

Actor ProphessorBFG : BFG9000 {
	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 25

	States {
		Ready:
			BFGG A 0 ACS_NamedExecute("HeroicKick", 0, 0)
			BFGG A 1 A_WeaponReady
			Loop
	}
}
