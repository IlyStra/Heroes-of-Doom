/**
*Copyright (c) 2018 PROPHESSOR
*
*This software is released under the MIT License.
*https://opensource.org/licenses/MIT
*/

// Arms in the hands of PROPHESSOR

Actor ProphessorFist : Fist {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"
}

Actor ProphessorKick : Kick {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.Kickback 228
	Inventory.PickupSound "prophessor/pickup"

	States {	
		Ready:
			KICK J 0 A_WeaponReady
			Goto Fire
		Deselect:
			KICK J 0 A_Lower
			Loop
		Select:
			KICK J 0 A_Raise
			Loop
		Fire:
			KICK K 1
			KICK L 1
			KICK M 1
			KICK N 1 A_PlaySound("weapon/kick", CHAN_5, 1.0, 0.0)
			KICK O 1
			KICK P 1 A_CustomPunch(0, false, 0, "BulletPuff", 64, 0, 0, "", "weapon/kickdown")
			KICK Q 1 A_CustomPunch(11, false, 0, "BulletPuff", 64, 0, 0, "", "prophessor/kick", "weapon/kickmiss")
			KICK R 3 A_Recoil(4)
			KICK S 1 
			KICK T 1 A_TakeInventory("ProphessorKick")
			Stop
	}
}

Actor ProphessorChainsaw : Chainsaw {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"
}

Actor ProphessorPistol : Pistol {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 100

	States {
		Fire:
			PISG A 4
			PISG B 1 A_FireBullets (5.6, 0, 1, 100, "BulletPuff")
			PISG C 1 A_GunFlash
			PISG C 15 A_SpawnItemEx("Backpack", 100, 0, 10, random(10, 20), 0, 0)
			PISG C 4
			PISG B 5 A_ReFire
			Goto Ready
	}
}

Actor ProphessorShotgun : Weapon 19345 {
	+NOALERT
	+INVENTORY.RESTRICTABSOLUTELY
	+WEAPON.AMMO_OPTIONAL

	Scale 0.85

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"
	Inventory.PickupSound "prophessor/pickup"
	Inventory.PickupMessage "Shotgun"

	Weapon.SelectionOrder 4500
	Weapon.KickBack 80
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Obituary "%o was blown away by %k using PROPHESSOR's Shotgun."

	States {
		Ready:
			SMAG A 1 A_WeaponReady
			Loop
		Deselect: 
			SMAG A 1 A_Lower
			Loop
		Select:
			TNT1 AAAAAAA 0 A_Raise(12)
			ASGW A 1
			ASGR ABCDE 1
			Loop
		Fire:
			TNT1 A 0 A_JumpIfNoAmmo("Empty")
			TNT1 A 0 A_PlaySound("prophessor/weapons/shotgun/fire", CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 Radius_Quake(3, 3, 0, 1, 0)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_Light1
			SMAF A 2 Bright A_FireBullets(5.0, 3.0, 7, Random(5, 7), "BulletPuff")
			SMAF B 2 Bright A_Light2
			TNT1 A 0 A_Light0
			SMAG BCDE 1
			SMAG FA 2
			TNT1 A 0 A_ReFire
			Goto Reload
		Empty:
			SMAG A 10 A_PlaySound("prophessor/weapons/shotgun/click", CHAN_WEAPON)
			Goto Ready
		Reload:   
			SMAK A 4
			SMAK B 4
			SMAK C 4
			SMAK D 4 A_PlaySound("prophessor/weapons/shotgun/reload", CHAN_WEAPON)
			SMAK E 4
			Goto Ready
		Spawn:
			SMAP A -1
			Stop
	}
}

Actor ProphessorSSG : SuperShotgun {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"
}

Actor ProphessorChaingun : Chaingun {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"

	States {
		Fire:
			CHGG AB 2 A_FireCGun
			CHGG B 0 A_ReFire
			Goto Ready
	}
}

Actor ProphessorRL : RocketLauncher {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Inventory.PickupSound "prophessor/pickup"

	States {
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_FireMissile
			MISG B 12 A_FireMissile
			MISG B 0 A_ReFire
			Goto Ready
	}
}

Actor ProphessorPlasmagun : PlasmaRifle {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 1
	Inventory.PickupSound "prophessor/pickup"

	States {
		Ready:
			MUS1 A 1 A_WeaponReady
			Loop
		Deselect:
			MUS1 A 1 A_Lower
			Loop
		Select:
			MUS1 A 1 A_Raise
			Loop
		Fire:
			MUS1 B 1 A_FirePlasma
			MUS1 CBCB 0 Bright A_Recoil( FRandom( 0.02, 0.35 ) )
			MUS1 C 0 A_ReFire
			Goto Ready
		Flash:
			MUS1 B 4 Bright A_Light1
			Goto LightDone
			MUS1 C 4 Bright A_Light1
			Goto LightDone
		Spawn:
			PLAS A -1
			Stop
	}
}

Actor ProphessorBFG : BFG9000 {
	+INVENTORY.RESTRICTABSOLUTELY

	Inventory.RestrictedTo "PROPHESSOR"
	Weapon.AmmoUse 25
	Inventory.PickupSound "prophessor/pickup"
}
