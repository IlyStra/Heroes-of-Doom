// Modified Nashgore DECORATE file

const int NASHGORE_BLOODSPOT_DURATION = 1050;
const int NASHGORE_BLOODFLAGS1 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY;
const int NASHGORE_BLOODFLAGS2 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION;
const int NASHGORE_BLOODFLAGS3 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE;

// Base blood
Actor Mayonez_BloodBase {
    Game Doom
    +CORPSE
    +DONTSPLASH
    +FORCEXYBILLBOARD
    +NOTELEPORT
    +NOBLOCKMONST
    +NOBLOCKMAP
    -SOLID
}

Actor Cum : Blood {
    Translation "0:255=80:95"
}

// Emulated regular Doom blood
Actor Mayonez_DoomBlood : Cum {
    +DONTSPLASH
    +FORCEXYBILLBOARD
}

// Modify the internal Blood actor to spawn flying blood
Actor Mayonez_Blood : Cum {
    -RANDOMIZE
    Translation "0:255=80:95"
    States {
        Spawn:
            TNT1 A 0
            TNT1 A 0 A_SpawnItem("Mayonez_DoomBlood", 0, 0, 0, 1)
            TNT1 A 0 A_SpawnItemEx("Mayonez_FlyingBlood", 0, 0, 0, FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.0, 6.0), 0, NASHGORE_BLOODFLAGS1, 64)
            TNT1 AAAAA 0 A_SpawnItemEx("Mayonez_SplatMaker", 0, 0, 0, FRandom(-5, 5), FRandom(-5, 5), FRandom(0, 5), Random(0, 360), NASHGORE_BLOODFLAGS2)
            TNT1 AAAA 4 A_SpawnItemEx("Mayonez_BloodSplasher", 0, 0, 0, FRandom(-2.0, 2.0), FRandom(-2.0, 2.0), FRandom(1.0, 2.0), Random(0, 360), NASHGORE_BLOODFLAGS1, 128)
            Stop
    }
}

// Blood actor that flies outwards and leaves trails behind
Actor Mayonez_FlyingBlood : Mayonez_BloodBase {
    Radius 8
    Height 2
    Gravity 0.25
    Translation "0:255=80:95"

    States {
        Spawn:
        FlyingNormal:
            TNT1 A 2 A_SpawnItem("Mayonez_FlyingBloodTrail", 0, 0, 0, 1)
            TNT1 A 0 A_Jump(12, "RandomlyDestroy")
            TNT1 A 0 A_Jump(27, "FlyingDecel")
            Loop
        FlyingDecel:
            TNT1 A 2 A_SpawnItem("Mayonez_FlyingBloodTrail", 0, 0, 0, 1)
            TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE)
            Loop
        RandomlyDestroy:
            TNT1 A 0
            Stop
        Crash:
            TNT1 A 0 A_PlaySound("bloodsplat")
            TNT1 A 0 A_SpawnItem("Mayonez_BloodSplash", 0, 0, 0, 1)
            TNT1 A 0 A_SpawnItemEx("Mayonez_BloodSpot", 0, 0, 0, 0, 0, 0, FRandom(0, 360), 7, 128)
            TNT1 AAAAAAAAAA 1 A_SpawnItemEx("Mayonez_BloodSplasher", 0, 0, 0, FRandom(-2.0, 2.0), FRandom(-2.0, 2.0), FRandom(1.0, 2.0), Random(0, 360), NASHGORE_BLOODFLAGS1, 150)
            Stop
    }
}

// Blood trails spawned by FlyingBlood
Actor Mayonez_FlyingBloodTrail : Mayonez_BloodBase {
    Radius 1
    Height 1
    Gravity 0.15
    Scale 0.55
    Translation "0:255=80:95"

    States {
        Spawn:
            CTRL ABCD 4 //bright
            Loop
        Crash:
            TNT1 A 0
            Stop
    }
}

// Blood splashers, generates Random splashes
Actor Mayonez_BloodSplasher : Mayonez_BloodBase {
    Radius 1
    Height 1
    Gravity 0.20
    Translation "0:255=80:95"

    States {
        Spawn:
            TNT1 A 1
            Loop
        Crash:
            TNT1 A 0 A_SpawnItem("Mayonez_BloodSplash", 0, 0, 0, 1)
            TNT1 A 0 A_SpawnItemEx("Mayonez_BloodSpot", 0, 0, 0, 0, 0, 0, Random(0, 360), NASHGORE_BLOODFLAGS3, 200)
            Stop
    }
}

// Blood spots that temporarily stay on the floor
Actor Mayonez_BloodSpot : Mayonez_BloodBase {
    -NOBLOCKMAP
    Radius 4
    Height 2
    RenderStyle Shaded
    Translation "0:255=80:95"
    StencilColor "6C 00 00"
    Alpha 0.85
    Scale 0.5
    States {
        Spawn:
            TNT1 A 0
            TNT1 A 0 A_SpawnItem("Mayonez_BloodSplash", 0, 0, 0, 1)
            TNT1 A 0 A_Jump(256, "BloodSpot1", "BloodSpot2", "BloodSpot3", "BloodSpot4")
        BloodSpot1:
            CSPT A 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
            "####" "#" 0 A_Jump(256, "FadeOut")
        BloodSpot2:
            CSPT B 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
            "####" "#" 0 A_Jump(256, "FadeOut")
        BloodSpot3:
            CSPT C 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
            "####" "#" 0 A_Jump(256, "FadeOut")
        BloodSpot4:
            CSPT D 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
            "####" "#" 0 A_Jump(256, "FadeOut")
        FadeOut:
            CSPT D 2 A_FadeOut(0.0005)
            Loop
    }
}

// Blood splashes
Actor Mayonez_BloodSplash : Mayonez_BloodBase {
    -NOBLOCKMAP
    Translation "0:255=80:95"

    Radius 1
    Height 2
    Scale 0.5
    States {
        Spawn:
            CSPL ABCDE 5
            Stop
    }
}

// Blood splat decal maker
// TO DO: This only generates red blood decals PLEASE FIX
// http://forum.zdoom.org/viewtopic.php?f=15&t=46146&p=774415#p774415
Actor Mayonez_SplatMaker {
    PROJECTILE

    Radius 4
    Height 8
    Translation "0:255=80:95"
    
    States {
        Spawn:
            TNT1 A 20
        Death:
            TNT1 A 1
            Stop
    }
}

// EOF